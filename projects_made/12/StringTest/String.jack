// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array s;
    field int maxLen;
    field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }
        let maxLen = maxLength;
        let length = 0;
        let s = Array.new(maxLength);
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do s.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return s[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let s[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < maxLen) {
            let s[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int index, intVal;
        var boolean sign;
        let intVal = 0;

        if ((length > 0) & (s[0] = 45)) { // check minus
            let sign = false;
            let index = 1;
        } else {
            let sign = true;
            let index = 0;
        }

        while (index < length) {
            let intVal = (intVal * 10) + s[index] - 48;
            let index = index + 1;
        }

        if (sign) {
            return intVal;
        } else {
            return -intVal;
        }
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0; // reset

        if (val < 0) {
            let val = -val;
            do appendChar(45); // append minus
        }
        do setIntCore(val);
        return;
    }

    method void setIntCore(int val) {
        var int nextVal;

        if (val < 10) {
            do appendChar(val + 48);
        } else {
            let nextVal = val / 10;
            do setIntCore(nextVal);
            do appendChar((val - (nextVal * 10)) + 48);
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
